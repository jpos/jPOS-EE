apply from: 'libraries.gradle'

allprojects {
    apply plugin: 'idea'
    apply plugin: 'eclipse'
    apply plugin: 'maven'

    group = 'org.jpos.ee'
    version = '2.2.3-SNAPSHOT'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'signing'
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    [ compileJava, compileTestJava, javadoc ]*.options*.encoding = 'UTF-8'
    def isSnapshot = true; // version.contains("SNAPSHOT")

    project.archivesBaseName="jposee-${project.name}"
    javadoc.failOnError = false

    def pomConfig = {
        name 'jPOS-EE Project'
        description """
    jPOS Extended Edition
  """
        url 'http://jpos.org'
        inceptionYear '1998'
        organization {
            name 'jPOS.org'
            url 'http://jpos.org'
        }
        issueManagement {
            system 'YouTrack'
            url 'https://jpos.org/issues'
        }
        scm {
            url "http://github.com/jpos/jPOS"
            connection "scm:git:https://github.com/jpos/jPOS-EE.git"
            developerConnection "scm:git:git@github.com:jpos/jPOS-EE.git"
        }
        licenses {
            license {
                name 'GNU AFFERO GENERAL PUBLIC LICENSE'
                url 'http://www.gnu.org/licenses/agpl-v3.html'
                comments 'See http://jpos.org/license for more details.'
                distribution 'repo'
            }
        }
        developers {
            developer {
                id 'jpos-team'
                name 'jPOS Development Team'
                email 'info@jpos.org'
            }
        }
    }

    configure(install.repositories.mavenInstaller) {
        pom.project pomConfig
    }
    signing {
        required { !isSnapshot }
        sign configurations.archives
    }
    uploadArchives {
        repositories {
            mavenDeployer {
                pom.project pomConfig
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
                repository(url: 'file:///opt/local/maven')
            }
        }
    }
    task sourceJar(type: Jar) {
        from sourceSets.main.allSource
        classifier = 'sources'
    }

    artifacts {
        archives sourceJar
    }

    repositories {
        mavenCentral()
        maven { url 'http://jpos.org/maven' }
        maven { url 'http://download.oracle.com/maven' }
        maven { url 'http://maven.vaadin.com/vaadin-addons' }
        mavenLocal()
    }

    dependencies {
        testCompile libraries.junit
    }
}

task allSources(type: Jar,dependsOn: subprojects.assemble) {
    baseName = 'jposee-sources'
    from subprojects.collect { project ->
        project.sourceSets.main.allSource
    }
}

task wrapper( type: Wrapper ) {
    gradleVersion = '3.0'
}

// Configure IDEA to use Git
idea.project.ipr {
  withXml { provider ->
    provider.node.component.find { it.@name == 'VcsDirectoryMappings' }.mapping.@vcs = 'Git'
  }
}

