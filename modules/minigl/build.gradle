
description = 'jPOS-EE :: MiniGL Module'

dependencies {
    api project(':modules:logback')
    api project(':modules:dbsupport')
    api libraries.commons_lang
    testImplementation project(':modules:db-h2')
    testImplementation project(':modules:db-postgresql')
    testImplementation libraries.postgresql_embedded
    testImplementation libraries.postgresql_bin
}

ext {
    testRuntimeDir = "$buildDir/runtime" as File
    testImportResourcesDir = new File(testRuntimeDir.parent,'test-classes')
}

task jposeeSetup(dependsOn: 'classes', type: JavaExec) {
    classpath = sourceSets.test.runtimeClasspath
    main = 'org.jpos.q2.install.Install'
    args = ["--outputDir=" + testRuntimeDir]
}

task copyTestResources(dependsOn: 'jposeeSetup', type: Copy) {
    from 'src/test/resources'
    into testImportResourcesDir
}

test {
    dependsOn 'copyTestResources'
    include '**/*Test.class'
    exclude '**/stress/*'
    workingDir testRuntimeDir
    systemProperty "test.minigl_db_driver", System.getProperty("test.minigl_db_driver")
    systemProperties(["user.name" : "travis" ])
}

