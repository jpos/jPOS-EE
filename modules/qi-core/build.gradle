plugins {
   id "fi.jasoft.plugin.vaadin" version "0.11.1"
}
vaadin {
    version=vaadinVersion
    addon.title='QI'
    manageDependencies = false
}
vaadinCompile.gwtSdkFirstInClasspath=false

apply plugin: 'maven-publish'

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
            artifact (sourceJar) {
                classifier = 'sources'
            }
            artifactId="jposee-${project.name}"
            pom.withXml {
                asNode().dependencies.'*'.findAll() {
                    it.scope.text() == 'runtime' && project.configurations.compile.allDependencies.find { dep ->
                       dep.name == it.artifactId.text()
                    }
                }.each() {
                    it.scope*.value = 'compile'
                }
                asNode().dependencies.'*'.findAll() {
                    if (it.groupId.text() == 'org.jpos.ee') {
                        it.artifactId*.value = "jposee-${it.artifactId.text()}"
                    }
                }
            }
        }
    }
    repositories {
        maven {
            url 'file:///opt/local/maven'
        }
    }
}

install { 
    doFirst {
        Configuration archivesConfig = project.getConfigurations().getByName('archives') 
        archivesConfig.artifacts.removeAll { 
            it.extension.equals('war')
        } 
    }
}
uploadArchives.enabled = false

dependencies {
    compile project(':modules:core')
    compile project(':modules:visitor')
    compile project(':modules:syslog')
    compile project(':modules:sysconfig')
    compile project(':modules:jetty')
    compile "com.vaadin:vaadin-server:${vaadinVersion}"
    compile "com.vaadin:vaadin-push:${vaadinVersion}"
    compile 'org.vaadin.addons:d3Gauge:1.0.1'
    compile 'org.vaadin.addons:dcharts-widget:1.7.0'
    compile 'org.vaadin.addons:vaadin-sliderpanel:1.3.0'
    compile "com.vaadin:vaadin-themes:${vaadin.version}"

    providedCompile "com.vaadin:vaadin-client:${vaadin.version}"
    providedCompile "com.vaadin:vaadin-client-compiler:${vaadin.version}"

    // providedCompile 'com.vaadin.external.slf4j:vaadin-slf4j-jdk14:1.6.1'
    // compile 'org.slf4j:jcl-over-slf4j:1.7.7'
    compile 'org.slf4j:jul-to-slf4j:1.7.7'
}

jar {
    from 'src/main/webapp'
    exclude '**/gwt-unitCache/*'
    exclude '**/gwt-unitCache'
}

jar.dependsOn("vaadinCompile")
install.dependsOn("publishToMavenLocal")
uploadArchives.dependsOn("publishMavenJavaPublicationToMavenRepository")

