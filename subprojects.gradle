apply plugin: 'java-library'
apply plugin: 'signing'

sourceCompatibility = 1.8
targetCompatibility = 1.8
[ compileJava, compileTestJava, javadoc ]*.options*.encoding = 'UTF-8'


java {
    withSourcesJar()
}

test {
    useJUnitPlatform()
    maxParallelForks = Runtime.runtime.availableProcessors()
}

dependencies {
    testImplementation libraries.junit
    //Java 10 support
    testImplementation('com.sun.xml.bind:jaxb-impl:2.1.2')
    testImplementation('com.sun.activation:javax.activation:1.2.0')
    testImplementation('javax.xml.bind:jaxb-api:2.3.0')
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            pom {
                name = 'jPOS Project'
                description = "jPOS Extended Edition"
                url = "http://jpos.org"
                organization {
                    name = 'jPOS.org'
                    url = 'http://jpos.org'
                }
                issueManagement {
                    system = 'Github Issues'
                    url = 'https://github.com/jpos/jPOS-EE/issues'
                }
                scm {
                    url = "http://github.com/jpos/jPOS-EE"
                    connection = "scm:git:https://github.com/jpos/jPOS-EE.git"
                    developerConnection = "scm:git:git@github.com:jpos/jPOS-EE.git"
                }
                licenses {
                    license {
                        name = 'GNU AFFERO GENERAL PUBLIC LICENSE'
                        url = 'http://www.gnu.org/licenses/agpl-v3.html'
                        comments = 'See http://jpos.org/license for more details.'
                        distribution = 'repo'
                    }
                }
                developers {
                    developer {
                        id = 'jpos-team'
                        name = 'jPOS Development Team'
                        email = 'info@jpos.org'
                    }
                }
            }

            groupId = 'org.jpos.ee'
            artifactId = 'jposee'
            from components.java
            artifactId="jposee-${project.name}"
        }
    }
} // publishing

signing {
    def isSnapshot = version.contains("SNAPSHOT")
    required { !isSnapshot }
    sign publishing.publications.mavenJava
}

task allDeps(type: DependencyReportTask) {}

